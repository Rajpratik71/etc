" 变量定义 {{{1
let s:WOLFWZR_colorscheme_applied = 0
let s:WOLFWZR_color_scheme = "none"
let s:WOLFWZR_backgroud = "light"
let s:WOLFWZR_cpu_info = system("grep -m 1 -o Atom /proc/cpuinfo | tr -d '\r\n'")
" }}}1

" 函数定义 {{{1
" WOLFWZR_set_tabline_style: 设置Tab栏 {{{2
function WOLFWZR_set_tabline_style()
    if ! has("gui_running") && $ITERM_PROFILE == "Dark"
        hi TabLine cterm=none ctermbg=black 
        hi TabLineSel cterm=bold ctermbg=blue ctermfg=white
        hi TabLineFill cterm=none ctermbg=black 
    endif
endfunction
" }}}2
" WOLFWZR_code_common_setting: 代码通用设置 {{{2
function WOLFWZR_code_common_setting()
    syntax on
    set cindent 
    set tabstop=4 
    set shiftwidth=4 
    set expandtab 

    if has("gui_running")
        if s:WOLFWZR_cpu_info == "Atom"
            set nocursorline
        else
            set columns=90
            set lines=37
            set cursorline
            "set colorcolumn=80
            "highlight WarningCols gui=undercurl
            "match WarningCols /.\%>80v/
        endif
    endif

    " 按<Leader>c执行make并打开quickfix窗口然后跳到第一个错误处(如果有错误的话)
    nnoremap <Leader>\ :make<CR><CR>:cw<CR>:cc<CR>
endfunction
"}}}2
" WOLFWZR_set_guifont: 设置GUI字体{{{2
function WOLFWZR_set_guifont()
    if !has("gui_running")
        return
    endif

    if has("macunix")
        set guifont=Monaco\ for\ Powerline:h16,Monaco:h16
        " 单独设置中文字体
        " 备用字体
        " Hannotate\ SC\ Regular
        " Libian\ SC\ Regular
        " Yuanti\ SC\ Regular
        " Xingkai\ SC\ Light
        set guifontwide=Kaiti\ SC\ Regular
    else
        if s:WOLFWZR_cpu_info == "Atom"
            set guifont=YaHei\ Consolas\ Hybrid\ for\ Powerline\ 12
                        \,YaHei\ Consolas\ Hybrid\ 12
        else
            set guifont=YaHei\ Consolas\ Hybrid\ for\ Powerline\ 14
                        \,YaHei\ Consolas\ Hybrid\ 14
        endif
    endif
endfunction
"}}}2
" WOLFWZR_set_colorscheme: 设置配色方案 {{{2
function WOLFWZR_set_colorscheme(color)
    " 条件检查 {{{3
    if (!has("gui_running")) || (s:WOLFWZR_color_scheme == a:color)
        return
    endif
    let s:WOLFWZR_color_scheme = a:color
    " }}}3
    " 为配合macosx夜间颜色反转，增加light主题{{{
    if has("macunix")
        let l:cur_hour = system("date +%H")

        if l:cur_hour >= 22 || l:cur_hour <= 8
            " 深夜OSX反转颜色，所有选用亮色背景，反转变成暗背景
            let s:WOLFWZR_backgroud = "light"
        else
            let s:WOLFWZR_backgroud = "dark"
        endif

        if s:WOLFWZR_backgroud == "light"
            " biogoo
            " bmichaelsen
            " cleanphp
            " colorful
            " colorzone
            " d8g_02
            " nedit
            " nedit2
            " pspad
            " smp
            " soso
            " [*****] neutron 
            " [*****] newspaper
            " [*****] oceanlight
            " [*****] peaksea
            " [*****] pyte
            " [*****] Tomorrow
            let s:WOLFWZR_color_scheme = "peaksea"
        endif
    endif
    "}}}
    " 设置配色方案及可能的修剪 {{{3
    " 备选配色方案："{{{
    " Monokai
    " PapayaWhip
    " Tomorrow-Night-Eighties
    " anotherdark
    " bubblegum
    " carvedwood
    " chance-of-storm
    " codeschool
    " corn
    " corporation
    " dante
    " darkburn
    " darkz
    " dusk
    " evening_2
    " freya
    " hybrid
    " lilypink
    " lucius
    " metacosm
    " mint
    " mrpink
    " neon
    " peaksea
    " pf_earth
    " phd
    " railscasts
    " rainbow_neon
    " rdark
    " selenitic
    " settlemyer
    " sonofobsidian
    " sorcerer
    " southwest-fog
    " spectro
    " strawimodo
    " symfony
    " tango-desert
    " tango2
    " tchaba
    " two2tango 
    " vilight
    " void
    " vydark
    " watermark
    " wolfasm (only aviliable in rmbp)
    " wombat
    " zenburn
    " kellys
    " mrpink
    " kib_darktango
    " kolor
    " lilypink
    " liquidcarbon
    " lucius
    " manuscript
    " native
    " nazca
    " southwest-fog
    "}}}
    execute 'colorscheme ' . s:WOLFWZR_color_scheme
    let s:WOLFWZR_colorscheme_applied = 1

    " 修剪{{{
    hi Cursor NONE
    hi Cursor gui=reverse

    if s:WOLFWZR_color_scheme == "Tomorrow-Night-Eighties"
        highlight Folded gui=none guibg=#515151 guifg=#99CC99
        if hlID("vimCommand") != 0
            highlight vimCommand guifg=#bbccaa
        endif
    elseif s:WOLFWZR_color_scheme == "rainbow_neon" || a:color == "neon"
        hi CursorLine guibg=Grey25
    elseif s:WOLFWZR_color_scheme == "gruvbox"
        hi cFunctions guifg=#ffcc88 cterm=bold ctermfg=DarkBlue
        hi CursorLine guibg=Grey25
    elseif s:WOLFWZR_color_scheme == "peaksea"
        hi CursorLine NONE 
        "hi CursorLine guibg=Grey77
    endif

    let l:WOLFWZR_tmp_color = synIDattr(hlID("Directory"), "fg", "gui")
    if l:WOLFWZR_tmp_color != ""
        execute 'highlight LineNr guifg='.l:WOLFWZR_tmp_color
    endif
    "}}}
    
    " }}}3
    " 重新加载mark.vim（否则mark.vim将失效） {{{3
    " 下面的autocmd命令无预期效果, why?
    " autocmd ColorScheme * source  ~wolfwzr/.vim/bundle/Mark/plugin/mark.vim
    source ~wolfwzr/.vim/bundle/Mark/plugin/mark.vim
    " }}}3
    " 更新YouCompleteMe弹出菜单样式 {{{3
    let l:WOLFWZR_tmp_color = synIDattr(hlID("CursorLine"), "bg", "gui")
    if l:WOLFWZR_tmp_color != ""
        execute 'highlight Pmenu guibg='.l:WOLFWZR_tmp_color
    endif

    let l:WOLFWZR_tmp_color = synIDattr(hlID("Normal"), "fg", "gui")
    if l:WOLFWZR_tmp_color != ""
        execute 'highlight Pmenu guifg='.l:WOLFWZR_tmp_color
    endif
        
    let l:WOLFWZR_tmp_color = synIDattr(hlID("Cursor"), "bg", "gui")
    if l:WOLFWZR_tmp_color != ""
        execute 'highlight PmenuSel gui=bold guibg='.l:WOLFWZR_tmp_color
    endif

    let l:WOLFWZR_tmp_color = synIDattr(hlID("Cursor"), "fg", "gui")
    if l:WOLFWZR_tmp_color != ""
        execute 'highlight PmenuSel gui=bold guifg='.l:WOLFWZR_tmp_color
    endif
    " }}}3
endfunction
" }}}2
" WOLFWZR_filetype_setting: FileType处理 {{{2
function WOLFWZR_filetype_setting()
    " C/CPP/H/NASL/LEX/YACC/BISON {{{3
    if &filetype == "c" 
       \ || &filetype == "h"
       \ || &filetype == "cpp"
       \ || &filetype == "nasl"
       \ || &filetype == "lex"
       \ || &filetype == "yacc"
       \ || &filetype == "bison"
        call WOLFWZR_code_common_setting()
        " 因为在cscope中跳转回来时打开的折叠又变为折叠，所以默认关闭折叠
        set foldmethod=syntax
        set nofoldenable

        " 用缩写(abbreivations)自定义代码块的补全
        " source ~/usr/etc/abbreviations.vim

        " gruvbox (要使这个主题生效，得先设置主题为greyblue)
        call WOLFWZR_set_colorscheme("greyblue")
        call WOLFWZR_set_colorscheme("gruvbox")

        if has("gui_running")
            winpos 200 20
            set lines=38
        endif
    " ASM {{{3
    elseif &filetype == "asm"
        call WOLFWZR_code_common_setting()
        call WOLFWZR_set_colorscheme("rainbow_neon")
        set syntax=nasm
        set foldmethod=marker
        set commentstring=;%s
        set nofoldenable
    " PYTHON {{{3
    elseif &filetype == "python"
        call WOLFWZR_code_common_setting()
        set foldmethod=indent
        set nofoldenable
    " JAVA {{{3
    elseif &filetype == "java"
        call WOLFWZR_code_common_setting()
    " SH {{{3
    elseif &filetype == "sh"
        call WOLFWZR_code_common_setting()
        set foldmethod=indent
        set nofoldenable
        call WOLFWZR_set_colorscheme("neon")
    " VIM {{{3
    elseif &filetype == "vim"
        call WOLFWZR_code_common_setting()
        call WOLFWZR_set_colorscheme("Tomorrow-Night-Eighties")
        set foldmethod=marker
        set nofoldenable
    " MKD {{{3
    elseif &filetype == "mkd"
        call WOLFWZR_code_common_setting()
        set nocindent
        set autoindent
        set nofoldenable
        call WOLFWZR_set_colorscheme("darktango")
    " HTML/XML/CSS/JS/XSL/XSLT {{{3
    elseif &filetype == "html"
        \ || &filetype == "xml"
        \ || &filetype == "css"
        \ || &filetype == "js"
        \ || &filetype == "xsl"
        \ || &filetype == "xslt"
        call WOLFWZR_code_common_setting()
        set nocindent
        set autoindent
        call WOLFWZR_set_colorscheme("Tomorrow-Night-Eighties")
    " TEXT {{{3
    elseif &filetype == "text"
        call WOLFWZR_set_colorscheme("Tomorrow-Night-Eighties")
    endif
    " }}}3
endfunction
" }}}2
" WOLFWZR_winpos_adjust: 调整GVIM窗口坐标 {{{2
function WOLFWZR_winpos_adjust(x_inc, y_inc)
    let l:winpos_x = getwinposx() + a:x_inc
    let l:winpos_y = getwinposy() + a:y_inc
    execute 'winpos '.l:winpos_x.' '.l:winpos_y
endfunction
" }}}2
" WOLFWZR_dictionary_qurey: 单词字典查询 {{{2
function WOLFWZR_dictionary_qurey(word)
    if a:word == ""
        return
    endif

    let l:tmp_file_prefix = "/tmp/vim_dict_"

    " 备份寄存器a
    let l:bakup_reg_a = @a

    if has("unix")
        " sdcv -n: 非交互模式
        let @a = system('sdcv -n --utf8-input --utf8-output '.a:word)
    else
        let @a = l:bakup_reg_a
        return
    endif

    let l:nr = bufwinnr(l:tmp_file_prefix . "*")
    if l:nr != -1
        execute nr . "wincmd w"
        normal ggVGd
    else
        " mktemp -u 只会生成文件名，不会创建文件
        execute "split" . system("mktemp -u ". l:tmp_file_prefix . "XXXXXXX")
    endif
    normal "ap

    nnoremap <buffer> q :q!<CR>
    nnoremap <buffer> <CR> 
        \ :call WOLFWZR_dictionary_qurey('<C-r>=expand("<cword>")<CR>')<CR>
    nnoremap <buffer> o
        \ :call WOLFWZR_dictionary_qurey('<C-r>=expand("<cword>")<CR>')<CR>
    " 恢复寄存器a
    let @a = l:bakup_reg_a
endfunction
" }}}2
" WOLFWZR_grep: grep {{{2
function WOLFWZR_grep(word)
    call setqflist([])
    silent execute "g/" . a:word . "/if &buftype == '' | call setqflist([{'type': 'l', 'col':1, 'bufnr': winbufnr('.'), 'lnum': line('.'), 'text':getline('.')}], 'a') | endif"
    botright cw
endfunction
" }}}2
" }}}1

" 基础设置 {{{1
" 设置显示相对行号，方便jk跳转 "{{{
set relativenumber
set number
"}}}
" runtimepath管理 "{{{
set runtimepath-=~wolfwzr/.vim
set runtimepath+=~wolfwzr/.vim
set runtimepath-=~wolfwzr/.vim/bundle/Vundle.vim
set runtimepath+=~wolfwzr/.vim/bundle/Vundle.vim
"}}}
" VI兼容性设置 {{{2
set nocp
" }}}2
" mapleader设置 {{{2
let mapleader="z"
" }}}2
" 搜索效果设置 {{{2
set hlsearch
set incsearch
" }}}2
" 设置新建窗口时新窗口的位置 {{{2
set splitbelow
set splitright
" }}}2
" 退格键设置(设置<BS>可删除的内容） {{{2
set backspace=indent,eol,start
" }}}2
" 文件编码设置 "{{{
set encoding=utf-8
set fileencodings=ucs-bom,utf-8,default,latin1
"}}}
" 状态栏设置 {{{2
set laststatus=2   " Always show the statusline
" 使用powerline代替
hi StatusLine cterm=bold gui=bold
set statusline=
    \%f\ %r\ %m\ %q
    \%=
    \[%{&fileformat},%{&fileencoding}%Y]
    \\ (%l/%L,%03v)\ %03P
" }}}2
" 打开文件时恢复光标位置 {{{2
if has("autocmd")
    autocmd BufReadPost * 
      \ if line("'\"") > 0 && line("'\"") <= line("$") | 
      \ exe "normal g`\"" | 
      \ endif 
endif
" }}}2
" 杂项设置{{{2
set wrap
" }}}2
" }}}1

" Vundle及插件设置入口 {{{1
" ~/.vim/bundle/Vundle.vim/Vundle.vimrc
" Vundle是一个管理插件的插件
" 这里配置文件里面包含了各个插件的配置
source ~wolfwzr/.vim/bundle/Vundle.vim/Vundle.vimrc
" }}}1

" 快捷键设置 {{{1
" 重设字体 (Bug?){{{
" 不知为何，当打开一些没有特殊处理的类型文件时
" （不在 WOLFWZR_filetype_setting 函数处理的类型）
" 偶尔会出现状态栏位于最底一行，把命令行给侵占了
" 通过debug发现重现设置字体能解决这问题。于是有了这个快捷键
" 但根本原因还是未找到，为何字体会造成这样的影响？
" nnoremap <Leader>\ :call WOLFWZR_set_guifont()<CR>
" }}}
" hardmode {{{2
" 用 <C-h> 代替 <BS>, <C-w> 还可以直接删除一个word
" 等熟练使用后可以将<BS>映射为删除整行 <ESC>^C
inoremap <BS> <NOP>
cnoremap <BS> <NOP>
" }}}2
" 翻页设置 {{{2
nnoremap <SPACE> <C-f>
nnoremap <TAB> <C-b>
" }}}2
" 快速查看QuickFix List的上一项/下一项 {{{2
if has("gui_running") && ! has("macunix")
    nnoremap <C-q> :botright cw<CR>
    nnoremap <C-n> :cn<CR>
    nnoremap <C-p> :cp<CR>
    " 提示:
    " :colder 查看上一个quickfix窗口
    " :cnewer 查看下一个quickfix窗口
endif
" }}}2
" Tab跳转 {{{2
nnoremap <Leader>j :tabnext<CR>
nnoremap <Leader>k :tabprev<CR>
" }}}2
" Window/GUI窗口相关{{{2
if has("gui_running") && ! has("macunix")
    " Window跳转 "{{{
    nnoremap <C-j> <C-w>j
    nnoremap <C-k> <C-w>k
    nnoremap <C-h> <C-w>h
    nnoremap <C-l> <C-w>l
    "}}}
    
    " Window大小调整 {{{
    " Decrease Window Height
    nnoremap <C-w>j <C-w>3-
    " Increase Window Height
    nnoremap <C-w>k <C-w>3+
    " Decrease Windows Width
    nnoremap <C-w>h <C-w>3<
    " Increase Windows Width
    nnoremap <C-w>l <C-w>3>
    " }}}

    " 调整GVIM窗口位置 {{{
    nmap <A-j> :call WOLFWZR_winpos_adjust(0,10)<CR>
    nmap <A-k> :call WOLFWZR_winpos_adjust(0,-10)<CR>
    nmap <A-h> :call WOLFWZR_winpos_adjust(-20,0)<CR>
    nmap <A-l> :call WOLFWZR_winpos_adjust(20,0)<CR>
    " }}}

    " 调整GVIM窗口大小 {{{
    nmap <A-S-j> :set lines+=1<CR>
    nmap <A-S-k> :set lines-=1<CR>
    nmap <A-S-h> :set columns-=2<CR>
    nmap <A-S-l> :set columns+=2<CR>
    "}}}
endif
" }}}
" 复制/剪切/粘贴 {{{2
if has("gui_running") && !has("macunix")
    nnoremap <Leader>c viw"+y
    vnoremap <Leader>c "+y
    inoremap <Leader>c <ESC>viw"+yea

    nnoremap <Leader>x viw"+x
    vnoremap <Leader>x "+x
    inoremap <Leader>x <ESC>viw"+xea
                     
    nnoremap <Leader>v "+p
    inoremap <Leader>v <C-r>+
    cnoremap <Leader>v <C-r>+
endif
" }}}2
" 代码折叠快捷键 {{{2
" nnoremap <Leader><SPACE> @=((foldclosed(line('.'))<0)?'zc':'zo')<CR>
" nnoremap <Leader><SPACE> za
" }}}2
" 切换输入法快捷键 {{{2
nnoremap <Leader><SPACE> <A-SPACE>
" }}}2
" 查看当前光标下单词的帮助信息 {{{2
" VIM的帮助信息 {{{3
nnoremap <Leader>hh :help <C-R>=expand("<cword>")<CR><CR>
nnoremap <Leader>hH :help <C-R>=expand("<cWORD>")<CR><CR>
" }}}3
" linux的man手册 {{{3
runtime ftplugin/man.vim
nnoremap K :Man <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm0 :Man 0 <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm1 :Man 1 <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm2 :Man 2 <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm3 :Man 3 <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm4 :Man 4 <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm5 :Man 5 <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm6 :Man 6 <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm7 :Man 7 <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm8 :Man 8 <C-r>=expand("<cword>")<CR><CR>
nnoremap <Leader>hm9 :Man 9 <C-r>=expand("<cword>")<CR><CR>
" }}}3
" 单词字典查询 {{{3
command -nargs=1 Sdcv call WOLFWZR_dictionary_qurey("<args>")
nnoremap <Leader>hd :Sdcv '<C-R>=expand("<cword>")<CR>'<CR>
" }}}3
" Buffer Grep {{{3
" 在当前Buffer中Grep
command -nargs=1 Grep call WOLFWZR_grep("<args>")
nnoremap <Leader>hg
    \ :call WOLFWZR_grep('<C-R>=expand("<cword>")<CR>')<CR>
" 在所有Buffer中Grep
nnoremap <Leader>hG
    \ :Bgrep <C-R>=expand("<cword>")<CR><CR>
" }}}3
" }}}2
" 快速预览当前光标下的 ColorScheme {{{2
noremap <Leader>C :color <C-r>=expand("<cWORD>")<CR><CR>
" }}}2
" VIMRC快速编辑及生效 {{{2
"nnoremap <Leader>ve :split ~/.vim/vimrc<CR>
"nnoremap <Leader>vs :source ~/.vim/vimrc<CR>
" }}}2
" cmdline模式下的光标移动 {{{2
if has("gui_running") && ! has("macunix")
    cnoremap <A-h> <Left>
    cnoremap <A-l> <Right>
    cnoremap <A-H> <Home>
    cnoremap <A-L> <End>
    " 跳到cmdline窗口 
    cnoremap <A-f> <C-f>

    cnoremap <C-j> <Down>
    cnoremap <C-k> <Up>
endif
" }}}2
" MacVim alt键映射 {{{2
if has("gui_running") && has("macunix")
    " MacVim无法映射alt键(<A-,<M-)都不行
    " 但可以通过直接输入<A-x>(在键盘上按住alt再按x键，得到一个乱码字符）得到
    " 于是可以通过 nmap ê <C-w>j 来创建映射
    " 但直接写在本文件中无效，在新文件中有效,所以将Mac下的alt映射放在新文件中
    " 猜测可能是因为本文件包含中文的原因，新文件中若不包含中文就有效
    source ~wolfwzr/.vim/mac-alt-map.vimrc
endif
" }}}2
" }}}1

" 文件类型相关设置 {{{1
if has("autocmd") 
    " 指定某些后缀的文件类型
    autocmd BufRead,BufNewFile *.{nasl,inc} set filetype=nasl
    autocmd BufRead,BufNewFile *.wsgi set filetype=python
    autocmd BufRead,BufNewFile *.bashrc set filetype=sh
    autocmd BufRead,BufNewFile *.vimrc set filetype=vim
    autocmd BufRead,BufNewFile *.{md,mkd,markdown} set filetype=mkd

    " 根据不同的文件类型做不同的设置
    autocmd VimEnter * call WOLFWZR_filetype_setting()
    nnoremap <Leader>w :call WOLFWZR_filetype_setting()<CR>

    " CommandLine Windows <CR>可以被映射成其它用途了，所以使用'o'来执行命令
    autocmd CmdWinEnter : nnoremap <buffer> o <CR>

    " 根据不同的文件类型设置按键映射
    augroup filetype_autocmd
        autocmd FileType c,cpp,h,flex,bison,yacc 
            \ nnoremap <buffer> <CR> :cs find g <C-R>=expand("<cword>")<CR><CR>|
            \ nnoremap <buffer> <BS> <C-t>
        autocmd FileType help 
            \ nnoremap <buffer> <CR> <C-]>| 
            \ nnoremap <buffer> o    <C-]>| 
            \ nnoremap <buffer> <BS> <C-t>|
            \ nnoremap <buffer> q :q<CR>
        autocmd FileType vim
            \ nnoremap <buffer> <CR> gf|
            \ nnoremap <buffer> <BS> <C-o>
        autocmd FileType mkd
            \ nnoremap <buffer> <F5> :!mkd_preview.sh %<CR>|
            \ nnoremap <buffer> o    A<CR>|
            \ inoremap <A-d> <CR><ESC>k:r !date +\%F<CR>kJJi<SPACE> |
            \ iabbrev <buffer> cdb^ ```<CR>```<ESC>ka|
            \ iabbrev <buffer> cd^ ``<ESC>i
        autocmd FileType qf
            \ nnoremap <buffer> o <CR>|
            \ nnoremap <buffer> q :q<CR>
        autocmd FileType man
            \ nnoremap <buffer> q :q<CR>|
            \ nnoremap <buffer> o :Man <C-r>=expand("<cword>")<CR><CR>|
            \ nnoremap <buffer> <CR> :Man <C-r>=expand("<cword>")<CR><CR>
    augroup end
endif
" }}}1

" GUI设置 {{{1
" GVIM {{{2
if has("gui_running")
    " 去掉菜单栏、工具栏等
    set go=

    call WOLFWZR_set_guifont()

    " 设置窗口位置
    if s:WOLFWZR_cpu_info == "Atom"
        winpos 120 0
    else
        winpos 245 30
    endif
    set columns=85
    set lines=37

    " 设置默认配色方案
    if s:WOLFWZR_colorscheme_applied == 0
        call WOLFWZR_set_colorscheme("Tomorrow-Night-Eighties")
    endif

    " 光标设置 (:help 'guicursor')
    set guicursor=n-v-c:block-Cursor/lCursor
    set guicursor+=ve:ver35-Cursor
    set guicursor+=o:hor50-Cursor
    set guicursor+=i-ci:ver25-Cursor/lCursor
    set guicursor+=r-cr:hor20-Cursor/lCursor
    set guicursor+=sm:block-Cursor
    set guicursor+=a:blinkon0
" TERMINAL {{{2
else 
    if s:WOLFWZR_colorscheme_applied == 0
        call WOLFWZR_set_colorscheme("delek")
    endif
endif
" }}}1

" cscope设置 {{{1
if has("cscope")
    set csprg=/usr/bin/cscope
    set csto=0
    set cst
	set cscopequickfix=s-,c-,d-,i-,t-,e-

    set nocsverb
    " add any database in current directory
    if filereadable("cscope.out") 
        cs add ./cscope.out
    " else add database pointed to by environment
    elseif $CSCOPE_DB != ""
        cs add $CSCOPE_DB
    endif
    set csverb

    nnoremap <Leader>fs :cs find s <C-R>=expand("<cword>")<CR><CR>
    nnoremap <Leader>fg :cs find g <C-R>=expand("<cword>")<CR><CR>
    nnoremap <Leader>fc :cs find c <C-R>=expand("<cword>")<CR><CR>
    nnoremap <Leader>ft :cs find t <C-R>=expand("<cword>")<CR><CR>
    nnoremap <Leader>fe :cs find e <C-R>=expand("<cword>")<CR><CR>
    nnoremap <Leader>ff :cs find f <C-R>=expand("<cfile>")<CR><CR>
    nnoremap <Leader>fi :cs find i <C-R>=expand("<cfile>")<CR><CR>
    nnoremap <Leader>fd :cs find d <C-R>=expand("<cword>")<CR><CR>

    command -nargs=1 Css cs find s <args>
    command -nargs=1 Csg cs find g <args>
    command -nargs=1 Csc cs find c <args>
    command -nargs=1 Cst cs find t <args>
    command -nargs=1 Cse cs find e <args>
    command -nargs=1 Csf cs find f <args>
    command -nargs=1 Csi cs find i <args>
    command -nargs=1 Csd cs find d <args>
endif
" }}}1

" vim: set ft=vim:
